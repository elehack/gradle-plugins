import org.gradle.api.publish.maven.MavenPublication

apply plugin: 'groovy'
apply plugin: 'maven'

group 'net.elehack.gradle'
jar.baseName = 'gradle-jruby'
version = '0.1-SNAPSHOT'

ext.isReleaseVersion = !version.endsWith("-SNAPSHOT")

configurations {
    wagon
}
repositories {
    mavenCentral()
    mavenLocal()
}
dependencies {
    compile gradleApi()
    compile localGroovy()
    wagon "org.apache.maven.wagon:wagon-ssh-external:2.2"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

artifacts {
    archives sourceJar, javadocJar
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.wagon
        if (project.hasProperty('deployUrl')) {
            repository url: deployUrl
        } else {
            repository url: 'scpexe://elehack.net/srv/www/maven/'
        }
    }
}