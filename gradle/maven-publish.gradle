apply plugin: 'signing'

ext.isReleaseVersion = !version.endsWith("-SNAPSHOT")

configurations {
    wagon
}
dependencies {
    wagon 'org.apache.maven.wagon:wagon-ssh-external:2.6'
    wagon 'org.apache.maven.wagon:wagon-webdav-jackrabbit:2.6'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

artifacts {
    archives sourceJar, javadocJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") && !project.hasProperty('skipSignatures') }
    sign configurations.archives
}

if (!project.hasProperty('deployUrl')) {
    ext.deployUrl = 'davs://elehack.net/maven/'
}
uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.wagon
        repository(url: deployUrl) {
            def user = System.getenv('DEPLOY_USER')
            if (user == null && project.hasProperty('deployUser')) {
                user = project.deployUser
            }
            def password = System.getenv('DEPLOY_PASSWORD')
            if (password == null && project.hasProperty('deployPassword')) {
                password = project.deployPassword
            }
            if (user != null && password != null) {
                authentication(userName: user, password: password)
            }
        }
        beforeDeployment {
            signing.signPom(it)
        }
    }
}
